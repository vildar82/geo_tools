(defun calculation-xy-gaus-to-bl (x y L0 a b / A* a_ B* B_ C* D* delta-lambda delta1 delta2 delta3 delta4 e etta etta_ f fi* ksi ksi_ l_ n)
  ; вычисляет географические B L (в радианах) по плоским координаты x y
  
;;;  (setq x 5728164.132)
;;;  (setq y -205079.975)
;;;  (setq L0 (convert-grad-rad 27.0))
;;;  (setq a 6378245.000)
;;;  (setq b 6356863.019)
;;;  (setq L0 (convert-grad-rad L0))
  (setq e (sqrt(/(-(expt a 2.0)(expt b 2.0))(expt a 2.0))))
  (setq f (/(- a b)a))
  (setq n (/ f (- 2.0 f)))
  (setq a_ (* (/ a (+ 1.0 n))(+ 1.0 (* (/ 1.0 4.0)(expt n 2.0))(* (/ 1.0 64.0)(expt n 4.0)))))
  (setq ksi (/ x a_))
  (setq etta (/ y a_))
  (setq delta1 (-(+(- (* (/ 1.0 2.0) n)(* (/ 2.0 3.0) (expt n 2.0)))(* (/ 37.0 96.0) (expt n 3.0)))(* (/ 1.0 360.0) (expt n 4.0))))
  (setq delta2 (-(+ (* (/ 1.0 48.0) (expt n 2.0))(* (/ 1.0 15.0) (expt n 3.0)))(* (/ 437.0 1440.0) (expt n 4.0))))
  (setq delta3 (-(* (/ 17.0 480.0) (expt n 3.0))(* (/ 37.0 840.0) (expt n 4.0))))
  (setq delta4 (* (/ 4397.0 161280.0) (expt n 4.0)))
  (setq ksi_ (- ksi
		(* delta1 (sin (* 2.0 ksi)) (cosh (* 2.0 etta)))
		(* delta2 (sin (* 4.0 ksi)) (cosh (* 4.0 etta)))
		(* delta3 (sin (* 6.0 ksi)) (cosh (* 6.0 etta)))
		(* delta4 (sin (* 8.0 ksi)) (cosh (* 8.0 etta)))))
  (setq etta_ (- etta
		(* delta1 (cos (* 2.0 ksi)) (sinh (* 2.0 etta)))
		(* delta2 (cos (* 4.0 ksi)) (sinh (* 4.0 etta)))
		(* delta3 (cos (* 6.0 ksi)) (sinh (* 6.0 etta)))
		(* delta4 (cos (* 8.0 ksi)) (sinh (* 8.0 etta)))))
  (setq fi* (asin (/ (sin ksi_) (cosh etta_))))
  (setq delta-lambda (atan (/ (sinh etta_)(cos ksi_))))
  (setq A* (+ (expt e 2.0)
	      (expt e 4.0)
	      (expt e 6.0)
	      (expt e 8.0)))
  (setq B* (* (* -1.0
		 (/ 1.0 6.0))
	      (+ (* 7.0 (expt e 4.0))
		 (* 17.0 (expt e 6.0))
		 (* 30.0 (expt e 8.0)))))
  (setq C* (* (/ 1.0 120.0)(+ (* 224.0 (expt e 6.0))(* 889.0 (expt e 8.0)))))
  (setq D* (* (* -1.0 (/ 1.0 1260.0))(* 4279.0 (expt e 8.0))))
  (setq L_ (+ L0 delta-lambda))
  (setq B_ (+ fi*(*
		   (* (sin fi*)(cos fi*))
		   (+ A*
		      (* B* (expt (sin fi*) 2.0))
		      (* C* (expt (sin fi*) 4.0))
		      (* D* (expt (sin fi*) 6.0))
		      )
		   )
	      )
	)
  
  (list B_ L_)
  )